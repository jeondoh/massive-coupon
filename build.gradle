import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '4.0.0' apply false
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.jeondoh'
version = '0.0.1-SNAPSHOT'
description = 'massive-coupon'

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.jeondoh'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'

    ext {
        JJWT_VERSION = "0.12.5"
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation "io.jsonwebtoken:jjwt-api:${JJWT_VERSION}"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.2.7.Final:osx-aarch_64'
        runtimeOnly "io.jsonwebtoken:jjwt-gson:${JJWT_VERSION}"
        runtimeOnly "io.jsonwebtoken:jjwt-impl:${JJWT_VERSION}"

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':core') {
    bootJar { enabled = false }
    jar { enabled = true }
}

project(':queue-core') {
    bootJar { enabled = false }
    jar { enabled = true }
}

project(':massive-router') {
    bootJar { enabled = true }
    jar { enabled = false }
}

project(':domain-queue') {
    bootJar { enabled = true }
    jar { enabled = false }
}

project(':api-server') {
    bootJar { enabled = true }
    jar { enabled = false }
}
